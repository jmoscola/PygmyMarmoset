// PygmyMarmoset gradle build script

///////////////////////////////////////////////////////////////////////
// Tasks
///////////////////////////////////////////////////////////////////////

task uberJar(
	type: Jar,
	dependsOn: [
		':copyWebappClasses',
		':copyConfigProperties',
		':copyLog4j2Properties',
		':copyWebappWebinf',
		':copyWebappFiles',
		':copyWebappJars',
		':copyLauncherFiles',
		':copyJettyDeps']) {
	manifest {
		attributes 'Main-Class' : 'edu.ycp.cs.pygmymarmoset.main.UberjarMain'
	}
    destinationDirectory = layout.buildDirectory.dir('.')
	archiveBaseName = 'pygmyMarmosetApp'
	from 'build/uberJarStaging'
}

// Copy all web app classes and resources into the uberjar staging area.
task copyWebappClasses(
	type: Copy,
	dependsOn: [
		':copyWebappProjectJars', // ensures depended-on classes are available
		':PygmyMarmosetWebapp:classes'
	]){
	from 'PygmyMarmosetWebapp/build/classes/java/main'
	into 'build/uberJarStaging/war/WEB-INF/classes'
	exclude "pygmymarmoset.properties"
}

task buildModelClassesJar(dependsOn: [':PygmyMarmosetModelClasses:jar']) { }

task buildPersistenceJar(dependsOn: [':buildModelClassesJar', ':PygmyMarmosetPersistence:jar']) {
	// persistence jar can't be built until model classes jar is built
	mustRunAfter ':buildModelClassesJar'
}

task buildControllersJar(dependsOn: [':buildPersistenceJar', ':PygmyMarmosetControllers:jar']) {
	// controllers jar can't be built until persistence jar is built
	mustRunAfter ':buildPersistenceJar'
}

// Copy jars of projects depended on by the webapp into the uberjar
// staging area.
task copyWebappProjectJars(
	type: Copy,
	dependsOn: [
		':buildControllersJar' // ensures model classes and persistence jars are built too
	]) {
	from 'PygmyMarmosetModelClasses/build/libs', 'PygmyMarmosetPersistence/build/libs',
					'PygmyMarmosetControllers/build/libs'
	into 'build/uberJarStaging/war/WEB-INF/lib'
	include '*.jar'
}

// Copy the configuration properties
task copyConfigProperties(type: Copy) {
	from project.projectDir
	into 'build/uberJarStaging/war/WEB-INF/classes'
	include 'pygmymarmoset.properties'
	
	// make sure there actually was a configuration properties file to copy
	if (inputs.sourceFiles.empty)
		throw new StopExecutionException("No pygmymarmoset.properties found")
}

// Copy the Log4j2 properties
task copyLog4j2Properties(type: Copy) {
	from project.projectDir
	into 'build/uberJarStaging'
	include 'log4j2.properties'
	
	// make sure there actually was a log4j2.properties file to copy
	if (inputs.sourceFiles.empty)
		throw new StopExecutionException("No log4j2.properties found")
}

// Copy the non-class and non-jar files in the webapp's WEB-INF
// into the uberjar staging area.
task copyWebappWebinf(type: Copy) {
	from 'PygmyMarmosetWebapp/war/WEB-INF'
	into 'build/uberJarStaging/war/WEB-INF'
	exclude 'classes/**', 'lib/**'
}

// Copy all other webapp files, such as JSPs, images, js, css, etc.
// into the uberjar staging area.
task copyWebappFiles(type: Copy, dependsOn: [':copyJstlTldFiles', ':copyJstlClassFiles']) {
	from 'PygmyMarmosetWebapp/war'
	into 'build/uberJarStaging/war'
	exclude 'WEB-INF/**'
}

// Copy all of the webapp's runtime jar files
// into the uberjar staging area.
task copyWebappJars(type: Copy) {
	from 'PygmyMarmosetWebapp/war/WEB-INF/lib'
	into 'build/uberJarStaging/war/WEB-INF/lib'
}

// Copy the launcher program classfiles into the uberjar staging area.
task copyLauncherFiles(type: Copy, dependsOn: [':PygmyMarmosetLauncher:classes']) {
	from 'PygmyMarmosetLauncher/build/classes/java/main'
	into 'build/uberJarStaging'
}

// Copy the jetty-logging.properties file
task copyJettyLoggingProperties(type: Copy) {
	// Jetty logging is currently not working as expected.  Would like Jetty to
	// direct all output to the Log4j2 implementation, but it isn't working yet.
	// Included 'log4j-appserver' as a dependency to get the following:
	// org.apache.logging.log4j.appserver.jetty.Log4j2Logger
	// But that doesn't seem to fix the issue.  Instead, Jetty seems to be using
	// it's default logger
	// This is a problem for another day.
	from project.projectDir
	into 'build/uberJarStaging'
	include 'jetty-logging.properties'

	// make sure there actually was a jetty-logging.properties file to copy
	if (inputs.sourceFiles.empty)
		throw new StopExecutionException("No jetty-logging.properties found")
}

// Copy all of the contents of the Jetty jarfiles into the uberjar staging area.
task copyJettyDeps(type: Copy, dependsOn: [':copyJettyLoggingProperties']) {
	duplicatesStrategy = 'INCLUDE'
	from requiredJars(project(':PygmyMarmosetLauncher')).collect { zipTree(it) }
	into 'build/uberJarStaging'
	exclude 'META-INF/**'
}

// Note: the following targets copy the .tld files and classfiles
// for Apache JSTL into the webapp.  Getting them to resolve
// correctly when running from the uberjar is complicated and
// would involve classloader hacks.  This solution is easy
// and works.

task explodeJstl(type: Copy) {
	from zipTree('Jetty9/lib/apache-jstl/taglibs-standard-impl-1.2.5.jar')
	into 'build/apache-jstl'
}

task copyJstlTldFiles(type: Copy, dependsOn: [':explodeJstl']) {
	from 'build/apache-jstl/META-INF'
	into 'build/uberJarStaging/war/WEB-INF'
	include '*.tld'
}

task copyJstlClassFiles(type: Copy, dependsOn: [':explodeJstl']) {
	from 'build/apache-jstl'
	into 'build/uberJarStaging/war/WEB-INF/classes'
	include 'org/**'
}

task clean(type: Delete) {
    delete rootProject.buildDir
} 


///////////////////////////////////////////////////////////////////////
// Common methods
///////////////////////////////////////////////////////////////////////

// Read an Eclipse classpath and return a list of jarfiles
// and directories.  See:
//   http://stackoverflow.com/questions/31653796
// I improved the basic technique so that referenced projects
// are added to the classpath correctly.
// WARNING: if there are projects with circular dependencies
// this method will execute an infinite recursion.
def parseEclipseClasspath(classpathFile, exportsOnly) {
	def baseDir = classpathFile.parentFile
	
	//println "classpathFile=${classpathFile}"
	def cpXml = new XmlParser().parse(classpathFile)
	def libPaths = []
	def addOutput = false
	def outputDir
	cpXml.classpathentry.each {
		if ('lib' == it.@kind) {
			// reference to jarfile
			def libPath = it.@path
			if (libPath.startsWith("/") && !exportsOnly) {
				// Jar file from another project: is referenced relative
				// to parent directory
				def parentDir = baseDir.parentFile.absolutePath
				libPaths.add(parentDir + libPath)
			} else {
				// Jar file is internal to this project
				def isExport = it.@exported == 'true'
				if (isExport || !exportsOnly) {
					libPaths.add(baseDir.path + '/' + libPath)
				}
			}
		} else if ('src' == it.@kind && !it.@path.startsWith('/') && exportsOnly) {
			// reference to source directory of depended-on project:
			// make a note to add the output directory
			addOutput = true
		} else if ('src' == it.@kind && it.@path.startsWith('/')) {
			// reference to project
			//println "Adding recursive dependencies from ${it.@path}"
			def refProjBase = new File(baseDir.path + '/..' + it.@path).canonicalPath
			//println "refProjBase=${refProjBase}"
			def refLibPaths = parseEclipseClasspath(new File(refProjBase + '/.classpath'), true)
			libPaths.addAll(refLibPaths)
		} else if ('output' == it.@kind) {
			outputDir = baseDir.path + '/' + it.@path
		}
	}
	
	if (addOutput) {
		// Add the output directory (i.e., compiled classfiles for this project)
		libPaths.add(outputDir)
	}
	
	return libPaths
}

// Get compile-time dependencies for specified project
def eclipseClasspath(p) {
	def baseDir = p.projectDir.absolutePath
	def classpathFile = new File(baseDir, '.classpath')
	//println "baseDir=${baseDir}"
	return parseEclipseClasspath(classpathFile, false)
}

// Get just the required jars for a project
def requiredJars(p) {
	return eclipseClasspath(p).findAll {
		it.endsWith('.jar')
	}
}
